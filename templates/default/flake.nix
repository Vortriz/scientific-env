{
    description = "Scientific env";

    inputs = {
        # set your systems using: https://github.com/nix-systems/nix-systems?tab=readme-ov-file#available-system-flakes
        systems.url = "github:nix-systems/x86_64-linux";

        nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
        flake-parts.url = "github:hercules-ci/flake-parts";
        import-tree.url = "github:vic/import-tree";
        devshell = {
            url = "github:numtide/devshell";
            inputs.nixpkgs.follows = "nixpkgs";
        };
        git-hooks = {
            url = "github:cachix/git-hooks.nix";
            inputs.nixpkgs.follows = "nixpkgs";
        };
        treefmt-nix = {
            url = "github:numtide/treefmt-nix";
            inputs.nixpkgs.follows = "nixpkgs";
        };
    };

    outputs =
        { flake-parts, ... }@inputs:
        flake-parts.lib.mkFlake { inherit inputs; } {
            systems = import inputs.systems;

            imports = [
                inputs.devshell.flakeModule
                inputs.treefmt-nix.flakeModule
                inputs.git-hooks.flakeModule

                (inputs.import-tree [ ./nix ])
            ];

            perSystem =
                { self', ... }:
                {
                    pre-commit.settings.hooks = {
                        flake-checker = {
                            enable = true;
                            after = [ "treefmt-nix" ];
                        };
                        treefmt = {
                            enable = true;
                            package = self'.formatter;
                        };
                    };

                    treefmt = {
                        # Used to find the project root
                        projectRootFile = "flake.nix";

                        programs = {
                            deadnix.enable = true;
                            statix.enable = true;
                            nixfmt.enable = true;
                        };

                        settings = {
                            global.excludes = [
                                # Exclude all files generated by nvfetcher
                                "**sources/*"
                            ];

                            formatter = {
                                deadnix.priority = 1;
                                statix.priority = 2;
                                nixfmt = {
                                    priority = 3;
                                    options = [ "--indent=4" ];
                                };
                            };
                        };
                    };
                };
        };
}
